buildscript {
    repositories {
        jcenter()

    }
    dependencies {
        //Check for the latest version here: http://plugins.gradle.org/plugin/com.jfrog.artifactory
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.4.10"
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.jfrog.artifactory'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 19
        versionCode 1
        versionName "1.0"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
        release {
            debuggable true
        }
        debug {
            debuggable true
        }
    }

}

group = 'com.edinaftc.ftc'
version = '1.0.0-alpha-SNAPSHOT'
status = 'SNAPSHOT'
def mArtifact = 'NinevoltLib'

repositories {
    flatDir {
        dirs '../libs'
    }
}

dependencies {
    compile(name: 'RobotCore-release', ext: 'aar')
    compile(name: 'Hardware-release', ext: 'aar')
    compile(name: 'FtcCommon-release', ext: 'aar')
    compile(name: 'Analytics-release', ext: 'aar')
    compile(name: 'WirelessP2p-release', ext: 'aar')
    compile(name: 'Analytics-release', ext: 'aar')
}

install {
    repositories.mavenInstaller {
        pom.project {
            name 'Ninevolt'
            description 'A utilities library for FTC'
            url 'https://github.com/edinaftc/Ninevolt'
            inceptionYear '2017'

            packaging 'aar'
            groupId project.group
            artifactId mArtifact
            version project.version
            licenses {
                license {
                    name 'The MIT License (MIT)'
                    url 'https://opensource.org/licenses/MIT'
                }
            }
            scm {
                connection 'https://github.com/edinaftc/Ninevolt.git'
                url 'https://github.com/edinaftc/Ninevolt'

            }
            developers {
                developer {
                    id = 'richiksc'
                    name 'Richik SC'
                }
                developer {
                    id = 'NickDim'
                    name 'Nick Dimitrov'
                }
            }
        }
    }
}

Properties bintrayProperties = new Properties()
bintrayProperties.load(project.rootProject.file('bintray.properties').newDataInputStream())

def bintrayUser = System.getenv("BINTRAY_USER") ?: bintrayProperties.getProperty('user')
def bintrayKey = System.getenv("BINTRAY_KEY") ?: bintrayProperties.get('key')

bintray {
    user = bintrayUser
    key = bintrayKey
    configurations = ['archives']
    pkg {
        repo = 'ftc'
        name = mArtifact
        userOrg = 'edinaftc'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/edinaftc/Ninevolt.git'
        publish = true
        version {
            name = project.version
            desc = 'Ninevolt FTC Library ' + project.version
            released = new Date()
            vcsTag = 'v' + project.version
        }
    }
}

configurations {
    published
}

task sourceJar(type: Jar) {
    classifier "sources"
    from 'src/main/java'
}

artifactoryPublish {
    dependsOn sourceJar
}

artifacts {
    published sourceJar
}

Properties artifactoryProperties = new Properties()
artifactoryProperties.load(project.rootProject.file('artifactory.properties').newDataInputStream())

def artifactoryUser = System.getenv("EDINAFTC_ARTIFACTORY_USER") ?: artifactoryProperties.getProperty('user')
def artifactoryPass = System.getenv("EDINAFTC_ARTIFACTORY_KEY") ?: artifactoryProperties.getProperty('password')

artifactory {
    contextUrl = artifactoryProperties.getProperty('contextUrl')   //The base Artifactory URL if not overridden by the publisher/resolver
    publish {
        repository {
            repoKey = 'libs-snapshot-local'
            username = artifactoryUser
            password = artifactoryPass
            maven = true

        }

        defaults {
            publishConfigs('archives', 'published')
            properties = ['build.status': project.status.toString()]
            publishPom = true // Publish generated POM files to Artifactory (true by default)
            publishIvy = false // Publish generated Ivy descriptor files to Artifactory (true by default)
        }
    }
    resolve {
        repository {
            repoKey = 'libs-snapshot'
            username = artifactoryUser
            password = artifactoryPass
            maven = true

        }
    }
}